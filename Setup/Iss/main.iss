; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Yet Another Duplicate File Remover"
#define MyAppVer "1.0.0"
#define MyAppPublisher "v_k softwares"
#define MyAppURL "http://yadfr.sourceforge.net/"
#define MyAppExeName "YADFR.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8D840E74-A760-487F-AE4E-A1B2CFDB28E1}
AppCopyright=Copyright (c) 2009 by violent_ken. Licensed under the GNU GPL, v3.0.
AppContact=maito:alaindescotes@gmail.com
AppName={#MyAppName}
AppVerName={#MyAppName} {#MyAppVer}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=..\..\YADFR\bin\BuildZipRelease\Bin\license.rtf
InfoBeforeFile=..\..\YADFR\bin\BuildZipRelease\Bin\README.txt
OutputBaseFilename=YADFR-v{#MyAppVer}-Setup
SetupIconFile=..\..\YADFR\icon.ico
Compression=lzma/ultra64
SolidCompression=yes
VersionInfoCompany=v_k softwares
VersionInfoCopyright=Licensed under the GNU GPL, v3.0.
VersionInfoProductName=YADFR
OutPutDir=..\..\RELEASE\

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Files]
Source: ..\..\YADFR\bin\BuildZipRelease\Bin\changelog.txt; DestDir: {app}; Flags: ignoreversion
Source: ..\..\YADFR\bin\BuildZipRelease\Bin\license.rtf; DestDir: {app}; Flags: ignoreversion
Source: ..\..\YADFR\bin\BuildZipRelease\Bin\README.txt; DestDir: {app}; Flags: ignoreversion
Source: ..\..\YADFR\bin\BuildZipRelease\Bin\{#MyAppExeName}; DestDir: {app}; Flags: ignoreversion
Source: ..\..\YADFR\bin\BuildZipRelease\Bin\help.rtf; DestDir: {app}\; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\{#MyAppExeName}
Name: {group}\{cm:ProgramOnTheWeb,{#MyAppName}}; Filename: {#MyAppURL}
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}
Name: {group}\{cm:HelpFile}; Filename: {app}\help.rtf
Name: {commondesktop}\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: quicklaunchicon

[Run]
Filename: {app}\{#MyAppExeName}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent
Filename: {win}\Microsoft.NET\Framework\v2.0.50727\ngen.exe; Parameters: "install ""{app}\{#MyAppExeName}"""; StatusMsg: {cm:Optimization}; Flags: runhidden runascurrentuser skipifdoesntexist

[Code]

// Initialization of setup
// Check if .Net framework 3.5 is present
function InitializeSetup(): Boolean;
var
	res : Boolean;
	errRes: Integer;
	winDir : String;
begin
	try
		// An exception will be raised if an attempt is made to expand this constant on a system with no .NET Framework version 2.0 present
		// dotnet20 = dotnet2032 if system is 32-bit, and = dotnet2064 if 64-bit
		ExpandConstant('{dotnet20}');
		Result := True;
	except
		begin
			res := MsgBox(ExpandConstant('{cm:noDotNetInstalled}'), mbCriticalError, MB_YESNO or MB_DEFBUTTON1) = IDYES;
			if res = False then
			begin
				Result := False;
			end
			else
			begin
				Result := False;
				ShellExec('open', 'http://download.microsoft.com/download/5/6/7/567758a3-759e-473e-bf8f-52154438565a/dotnetfx.exe', '', '', SW_SHOWNORMAL, ewNoWait, errRes);
			end;
		end;
	end;
end;

[CustomMessages]
noDotNetInstalled =Yet Another Duplicate File Remover {#MyAppVer} requires Microsoft .Net Framework 2.0 to work. Please install .Net Framework 2.0 and then try to install Yet Another Duplicate File Remover again. Would you like to download .Net Framework 2.0 now ?
OtherTasks =Other tasks
Optimization =Optimizing Yet Another Duplicate File Remover...
HelpFile=Help